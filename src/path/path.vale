import stdlib.collections.*;

export Array<imm, str> as StrArray;
export Array<mut, str> as MutStrArray;
export List<Path> as PathList;


interface FileError { }
struct FileNotFoundError {
  path Path;
}
impl FileError for FileNotFoundError;


exported fn AddToPathChildList(parent &Path, list &!List<Path>, child_name str) {
  list!.add(parent./(child_name));
}

exported struct Path {
  segments List<str>;
  //drive Opt<str>;
}
fn resolve(self &Path) Path {
  Path(resolve(self.str()))
}

fn is_dir(path &Path) bool {
  is_dir(path.str())
}

fn is_file(path &Path) bool {
  is_file(path.str())
}

fn parent(path &Path) Path {
  result = path.clone();
  if (result.segments.len() == 0) {
    panic("Can't get parent directory, is already root.");
  }
  result.segments!.remove(result.segments.len() - 1);
  ret result;
}

fn directory(path &Path) Path {
  result = path.clone();
  if (path.is_dir()) {
    ret result;
  }
  result.segments!.remove(result.segments.len() - 1);
  ret result;
}

fn Path(s str) Path {
  segments = List<str>();
  foreach a in s.split("/") {
    foreach b in a.split("\\") {
      segments!.add(b.str());
    }
  }
  // If it ends in a / there will be an empty string at the end, remove it.
  if (segments.get(segments.len() - 1) == "") {
    segments!.remove(segments.len() - 1);
  }
  vassert(segments.len() > 0, "Path is empty!");
  ret Path(segments);
}

fn name(path &Path) str {
  path.segments.get(path.segments.len() - 1)
}

fn str(path &Path) str {
  sep = GetPathSeparator();

  builder = StringBuilder();
  first = true;
  //if (not path.drive.isEmpty()) {
  //  builder!.print(path.drive.get());
  //  set first = false;
  //}
  foreach segment in path.segments {
    if (not first) {
      builder!.print(sep);
    }
    builder!.print(segment);
    set first = false;
  }
  ret builder.str();
}

fn exists(path &Path) bool {
  ret path.str().exists();
}

fn ==(a &Path, b &Path) bool {
  if (a.segments.len() != b.segments.len()) {
    ret false;
  }
  i = 0;
  while (i < a.segments.len()) {
    if (a.segments.get(i) != b.segments.get(i)) {
      ret false;
    }
    set i = i + 1;
  }

  //if (a.drive.isEmpty() != b.drive.isEmpty()) {
  //  ret false;
  //}
  //if (not a.drive.isEmpty()) {
  //  if (a.drive.get() != b.drive.get()) {
  //    ret false;
  //  }
  //}
  ret true;
}
fn !=(a &Path, b &Path) bool { not (a == b) }

fn clone(self &Path) Path {
  segments = List<str>();
  foreach segment in self.segments {
    segments!.add(segment);
  }
  ret Path(segments); //, self.drive);
}

fn /(path &Path, segment str) Path {
  new_path = path.clone();

  new_segments = segment.split(GetPathSeparator());
  foreach new_segment in new_segments {
    if (new_segment.len() > 0) {
      new_path.segments!.add(new_segment.str());
    }
  }

  ret new_path;
}

fn iterdir(path &Path) List<Path> {
  destination_list = List<Path>();
  success = iterdir(path, path.str(), &!destination_list);
  if (not success) {
    panic("iterdir failed!");
  }
  ret destination_list;
}

fn readAsString(path &Path) str {
  readFileAsString(path.str())
}
fn writeString(path &Path, contentsVStr str) {
  writeStringToFile(path.str(), contentsVStr)
}

fn CreateDir(path &Path, allow_already_existing bool) Result<void, FileError> {
  if (CreateDirExtern(path.str(), allow_already_existing)) {
    Ok<void, FileError>(void())
  } else {
    panic("Error in CreateDir")
  }
}

fn CreateDirAll(path &Path, allow_already_existing bool) Result<void, FileError> {
  if (path.segments.len() == 0) {
    ret Err<void, FileError>(FileNotFoundError(path.clone()));
  }
  if (not path.exists()) {
    // Recurse
    recurse_result = CreateDirAll(&path.parent(), allow_already_existing);
    if (recurse_result.is_err()) {
      ret recurse_result;
    }
    recurse_result.expect();

    create_dir_result = path.CreateDir(allow_already_existing);
    if (create_dir_result.is_err()) {
      ret create_dir_result;
    }
    create_dir_result.expect();
  }
  ret Ok<void, FileError>(void());
}

extern fn readFileAsString(filenameVStr str) str;
extern fn writeStringToFile(filenameVStr str, contentsVStr str) void;
extern fn GetEnvPathSeparator() str;
extern fn GetPathSeparator() str;
extern fn is_dir(value str) bool;
extern fn is_file(value str) bool;
extern fn exists(path str) bool;
extern fn iterdir(path &Path, path_str str, destination_list &!List<Path>) bool;
extern fn makeDirectory(path str, allow_already_existing bool) bool;
extern fn resolve(path str) str;
extern fn CreateDirExtern(path str, allow_already_existing bool) bool;
